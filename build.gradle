/**
* To create the Eclipse project and classpath files:
*  ./gradlew cleanEclipse eclipse
*
* To build the PojoBuilder:
*  ./gradlew build
*
* To publish a new release of PojoBuilder to Sonatype OSS Maven Repo:
*  ./gradlew clean uploadArchives
*
*/

task wrapper(type: Wrapper) { gradleVersion = '2.0' }

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
  }
}

if (!project.hasProperty('ossrhUsername') || !project.hasProperty('ossrhPassword')) {
  println 'OSS repository hosting username or password not set'
  ext.ossrhUsername = ''
  ext.ossrhPassword = ''
}

ext {
  isReleaseVersion = !version.endsWith('SNAPSHOT')
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

group = 'net.karneim'
archivesBaseName = "pojobuilder"
version = '3.3.1'
sourceCompatibility = 1.6

repositories.mavenCentral()

dependencies {
  compile 'com.squareup:javawriter:2.5.0'
  testCompile 'junit:junit:4.11'
  testCompile 'org.assertj:assertj-core:1.6.1'
  testCompile 'com.google.guava:guava:15.0'
}

jacocoTestReport {
  reports {
    xml.enabled = true
    html.enabled = true
  }
}

eclipse {
  classpath {
    downloadSources = true
    downloadJavadoc = false
  }
}

tasks.withType(JavaCompile) { options.encoding = "UTF-8" }

sourceSets {
  test {
    java {
      srcDir 'src/testdata/java'
      srcDir 'src/test/java'
    }
  }
}

jar {
  from 'src/jar/resources'
}

task dependenciesJar(type: Jar, dependsOn:['compileJava']) {
  classifier = "jar-with-dependencies"
  from files(sourceSets.main.output.classesDir)
  from files(sourceSets.main.output.resourcesDir)
  from 'src/jar/resources'
  from {configurations.compile.collect {zipTree(it)}}
  into('META-INF/license') {
  	from 'license'
  }
}

task annotationsJar(type: Jar, dependsOn:['compileJava']) {
  classifier = "annotations"
  from files(sourceSets.main.output.classesDir)
  includeEmptyDirs false
  include '**/GeneratePojoBuilder.class'
  include '**/FactoryProperties.class'
  from files(sourceSets.main.output.resourcesDir)
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

tasks.withType(Jar) {
  into('META-INF') {
    from 'COPYING'
  }
  manifest {
    attributes(
      'Implementation-Title': 'PojoBuilder',
      'Implementation-Version': version
    )
  }
}

artifacts {
  archives dependenciesJar, annotationsJar, javadocJar, sourcesJar
}

signing {
  required { isReleaseVersion && gradle.taskGraph.hasTask('uploadArchives') }
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
      beforeDeployment {
        if ( project.ossrhUsername == "" || project.ossrhPassword == "")
          throw new GradleException('Missing ossrhUsername or ossrhPassword');
      }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'PojoBuilder'
        packaging 'jar'
        description 'The PojoBuilder Generator is a Java 6 compliant annotation processor that generates a fluent builder class for POJOs (Plain Old Java Object)'
        url 'http://github.com/mkarneim/pojobuilder'

        scm {
	      url 'git@github.com:mkarneim/pojobuilder.git'
	      connection 'scm:git:git@github.com:mkarneim/pojobuilder.git'
	      developerConnection 'scm:git:git@github.com:mkarneim/pojobuilder.git'
	      tag 'HEAD'
	    }
        licenses {
          license {
            name 'PUBLIC DOMAIN'
            url 'http://github.com/mkarneim/pojobuilder/blob/master/COPYING'
            distribution 'repo'
            comments 'Do-whatever-you-want license'
          }
        }
        developers {
          developer {
            id 'mkarneim'
            name 'Michael Karneim'
            timezone 'GMT+1'
          }
        }
      }
    }
  }
}
